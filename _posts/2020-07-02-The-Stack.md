---
layout: post
title:  "The Stack"
---

## Introduction
The Stack is a memory region designated to store the data for functions runtime, the way the stack is organized and used is dependent on the specific ABI the program was compiled for.
The way I chose to approach this topic is through examples.

First have a look at the following piece of C code:
```c
int add(int a, int b) {
	int x = a; 
	int y = b;
 
	return x + y;
}

int main(int argc, char **argv) {
	add(4, 5);

	return 0;
}
```

### x86 Walk Through
### Passing Arguments
As seen above, the add function takes 2 integers as arguments, the caller function, main, pushes the function’s arguments onto the stack, last parameter pushed first. Like so:

```asm
push 5
push 4
```

> What does push do?
>> Decrements the stack pointer and then stores the source operand on the top of the stack.

![StackFirstStage](/assets/img/stack_first_stage.png)

# Calling the function
Caller calls the function using the call operator

```asm
call add
```

> What does call do?
>> Pushes the memory location of the next instruction (after the call) on top of the stack, which will later represent the return address of the called function. And jumps to the memory location given as an operand (the add function in this case) by setting the EIP register to it.


![StandSecondStage](/assets/img/stack_second_stage.png)

Inside The function - The function prologue
The functions prologue is built of 3 instructions responsible for setting the function stack frame

```asm
push ebp
mov ebp, esp
sub esp, 8
```

First, saving the caller’s ebp register by pushing it on top of the stack, then setting the new stack frame base pointer by moving the value from the esp register to the ebp register, and lastly subtracting esp by 8 thus creating a memory area on the stack of size for local variables - two integers.

> What is esp for?
>> The esp register holds the memory address pointing to the top of the stack at any time. Every push/pop instruction will manipulate the esp register accordingly.

> What is ebp for?
>> The ebp register is responsible for the stack frame base pointer, it points to the location in memory that the stack frame begins at. every access to the stack within the function is relative to the stack base pointer. In x86 ABI the stack grows downwards, so accessing function arguments will look like `[ebp+0x8]` - first argument, `[ebp+0xC]` - second argument, and so on..
Accessing local variables will look like `[ebp-0x4]` - first local variable, `[ebp-0x8]` - second local variable, and so on..
Everything access is relative to the stack base pointer. Its important to note that some functions are what is called esp based, meaning they will access their arguments and local variables through the esp register.

> What does sub do?
>> Subtracts the second operand from the first operand and stores the result in the destination operand.

> What does mov do?
>> Copies the second operand to the first operand. The source operand can be an immediate value, general-purpose register, segment register, or memory location; the destination register can be a general-purpose register, segment register, or memory location. Both operands must be the same size, which can be a byte, a word, a doubleword, or a quadword.

And now the stack will look like this, where each block represents 4 bytes, aka DWORD:

![StackThirdStage](/assets/img/stack_third_stage.png)

Saving local variables
Function arguments and local variables can be accessed relative to ebp or esp, and their value can be moved using a general purpose register.

```asm
mov eax. [ebp+8] ; move first argument into eax
mov [ebp-4], eax ; move value from eax into first local variable
mov eax. [ebp+12] ; move second argument into eax
mov [ebp-8], eax ; move value from eax into second local variable
```

![StackFourthStage](/assets/img/stack_fourth_stage.png)

Adding the values together
Using general purpose registers we can add values together

```asm
mov edx, [ebp-4] ; move first local variable into edx
mov eax, [ebp-8] ; move second local variable into eax
add eax, edx ; add them together
```


> What does add do?
>> Adds the first operand and the second operand and then stores the result in the first operand.

## Returning from the function - The function epilogue
Once the function’s logic is done we can return control to the calling function, we do so by making sure our return value is stored in the eax register (the add operator from section 5 stored the result in eax). And run the function epilogue:

 
```asm
leave
ret
```

The function epilogue is responsible for safely removing the function’s stack frame so that the caller function can proceed execution without any problems.
Going back to the function prologue we saved the caller’s ebp and then subtracted esp value according to the amount of memory we needed to allocate for our local variables.
Now we do the exact opposite procedure, freeing the memory allocated on the stack for the local variables by changing esp’s value to ebp’s value, and returning ebp’s value to the caller’s original ebp value by using the saved ebp value and popping it back to the ebp register.
Then we need to jump back to the caller’s code, we use the saved returned address for this by popping it off the stack and jumping to it.

> What does leave do?
>> Setting esp’s value to ebps’ value and then popping the top of the stack into ebp (returning saved ebp)

> What does ret do?
>> Popping the return address off the stack and jumping to it.

What the stack looks like now:

![StackFifthStage](/assets/img/stack_fifth_stage.png)

### The last step - Caller continues execution
The last step is done by the caller, just as the caller is responsible for pushing the arguments on top the stack it is also responsible for popping them.

```asm
add esp, 8
```

As we recall the add function takes 2 integers as arguments, a total of 8 bytes so in order to free that memory we increment esp by 8, and now the top of the stack is 8 bytes “back”.


And we are done.